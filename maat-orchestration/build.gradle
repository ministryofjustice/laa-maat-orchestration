plugins {
	id "java"
	id "jacoco"
	id "org.sonarqube" version "4.4.1.3373"
	id "info.solidsoft.pitest" version "1.15.0"
	id "org.springframework.boot" version "3.1.5"
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.jsonschema2dataclass" version "6.0.0"
}

group = "uk.gov.justice.laa.crime"

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

def versions = [
		pitest                  : "1.15.4",
		springdocVersion        : "2.3.0",
		crimeCommonsVersion     : "2.19.1",
		okhttpVersion           : "4.12.0",
		wmStubRunnerVersion    	: "4.1.0"
]

configurations {
	configureEach {
		exclude group: 'junit', module: 'junit'
	}
}

repositories {
	mavenCentral()
}

dependencies {

	implementation "uk.gov.justice.service.laa-crime:crime-commons-spring-boot-starter-rest-client:$versions.crimeCommonsVersion:all"
	implementation "uk.gov.justice.service.laa-crime:crime-commons-classes:$versions.crimeCommonsVersion"

	implementation "io.micrometer:micrometer-registry-prometheus"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.springdocVersion"

	implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.boot:spring-boot-starter-validation"

	compileOnly "org.projectlombok:lombok"
	annotationProcessor "org.projectlombok:lombok"

	testImplementation "org.pitest:pitest:$versions.pitest"
	testImplementation "org.springframework.security:spring-security-test"
	testImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner:$versions.wmStubRunnerVersion"
}

jacoco {
	toolVersion = "0.8.8"
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"**/dto/**",
					"**/config/**",
					"**/model/**"
			])
		}))
	}
}

pitest {
	targetClasses = [
			"uk.gov.justice.laa.maat.orchestration.*",
			"uk.gov.justice.laa.maat.orchestration.controller.*",
			"uk.gov.justice.laa.maat.orchestration.validation.*"
	]
	excludedClasses = [
			"uk.gov.justice.laa.maat.orchestration.dto.*",
			"uk.gov.justice.laa.maat.orchestration.mapper.*",
			"uk.gov.justice.laa.maat.orchestration.MAATOrchestrationApplication"
	]
	threads = 5
	outputFormats = ["XML", "HTML"]
	timestampedReports = false
	mutationThreshold = 60
}


tasks.named("sonar") {
	it.dependsOn test
}

sonar {
	properties {
		property "sonar.exclusions", "**/dto/**.java , **/model/**.java, **/config/**.java, **/enums/**.java, **/exception/**.java"
		property "sonar.coverage.exclusions", "**/MAATOrchestrationApplication.java, **/model/**.java"
		property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
		property "sonar.projectKey", "ministryofjustice_laa-maat-orchestration"
		property "sonar.organization", "ministryofjustice"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.projectName", "Crime Apps :: maat-orchestration"
		property "sonar.pitest.mode", "reuseReport"
		property "sonar.pitest.reportsDirectory", "build/reports/pitest"
	}
}

jsonSchema2Pojo {
	executions {
		main {
			methods.builders = true
			methods.buildersDynamic = true
			methods.annotateJsr303Jakarta = true
			fields.floatUseBigDecimal = true
			dateTime.dateTimeType = "java.time.LocalDateTime"
			klass.targetPackage = "uk.gov.justice.laa.crime.orchestration.model"
			io.source.setFrom files("${project.rootDir}/src/main/resources/schemas")
		}
	}
}
